# ALU の設計 その 5：桁上げ先見加算器の実装
前回は、通常の順次桁上げ加算器よりも高速な桁上げ先見加算器、およびプリフィックス加算器について学びました。
今回は、桁上げ加算器の実装を見ていきます。

## 加算器ブロックの作成
まず、$N$ ビットの加算器を分割した加算器のブロックを実装します。
```n``` 個の全加算器を含む回路の実装は以下の通りです。

```carry_lookahead_adder.sv```
```systemverilog
`include "fulladder.sv"

module adder_block
    #(parameter NBIT = 4)
    (
        input  logic [(NBIT - 1):0] a, b,
        input  logic cin,
        output logic [(NBIT - 1):0] s,
        output logic cout
    );
    // 各全加算機の出力信号
    logic [(NBIT - 1):0] fa_cout;

    // 生成信号と伝播信号
    logic [(NBIT - 1):0] g, p;
    logic [(NBIT - 1):0] block_g, block_p;

    genvar i;
    generate
        // NBIT 個の全加算器と生成・伝播信号の演算
        for (i = 0; i < NBIT; i++) begin : genAdderBlock
            fulladder fa(
                .a(a[i]), .b(b[i]), .cin(i == 0 ? cin : fa_cout[i - 1]), 
                .s(s[i]), .cout(fa_cout[i])
            );
            assign g[i] = a[i] & b[i];
            assign p[i] = a[i] | b[i];
            assign block_g[i] = i == 0 ? g[0] :  g[i] | (p[i] & block_g[i - 1]);
            assign block_p[i] = i == 0 ? p[0] :  p[i] & block_p[i - 1];
        end
        assign cout = block_g[NBIT - 1] | (block_p[NBIT - 1] & cin);
    endgenerate
endmodule
```

基本的な部分は桁上げ伝播加算器と同じで、生成信号 $G$ と伝播信号 $P$ の演算回路が追加されています。
$G_{i:0}$ や $P_{i:0}$ は桁上げ伝播加算器の演算が完了してからまとめて演算するのではなく、全加算器が1段進むごとに1段ずつ計算するように実装しています。

```systemverilog
// 生成信号と伝播信号
logic [(NBIT - 1):0] g, p;
logic [(NBIT - 1):0] block_g, block_p;

genvar i;
generate
    // NBIT 個の全加算器と生成・伝播信号の演算
    for (i = 0; i < NBIT; i++) begin : genAdderBlock
        ...

        assign g[i] = a[i] & b[i];
        assign p[i] = a[i] | b[i];
        assign block_g[i] = i == 0 ? g[0] :  g[i] | (p[i] & block_g[i - 1]);
        assign block_p[i] = i == 0 ? p[0] :  p[i] & block_p[i - 1];
    end
    assign cout = block_g[NBIT - 1] | (block_p[NBIT - 1] & cin);
endgenerate
```

1つのブロックに含まれる全加算器の数は任意に設定できますが、通常4個ごとにブロック分けします。
なぜなら、それが計算速度と回路規模のバランスが一番良いからです。
全ての入力ビットについて生成・伝播信号を計算すれば、全加算器の計算を全て並列化できますが、その分、生成・伝播信号の演算回路規模が増大します。
一方、ブロック1つ当たりの全加算器の数を増やしすぎると、ブロック内の演算遅延が大きくなります。
そのため、全加算器を4個ずつブロック分けしているのです。

## 加算器ブロックのテスト
4ビットの加算器ブロックを作成し、期待通りの出力が得られるかテストします。
これまでは、全パターンをハードコーディングして目視で出力が正しいかを確認していました。
しかし、目視での確認には限界がありますし、パターン数が増えたときに全パターンを網羅するのは難しくなります。
そこで、今回は SystemVerilog の乱数生成機能を利用して、ランダムに生成したパターンで計算結果が正しくなることを確認してみます。
以下が、これを実現するコードです。

```test_carry_lookahead_adder.sv```
```systemverilog
`include "carry_lookahead_adder.sv"

module test_adder_block();
    logic [3:0] a, b;
    logic [3:0] s;
    logic       cin, cout;
    logic [4:0] expected;

    // 4ビット加算器のインスタンスを作成
    adder_block #(.NBIT(4)) _adder (.a(a), .b(b), .cin(cin), .s(s), .cout(cout));

    // 計算結果が正しいかチェックする
    task check(
        input logic [3:0] a, 
        input logic [3:0] b,
        input logic       cin
    );
        expected = a + b + cin;
        if (s + cout * 16 == expected)
            $display("%2d %2d  %d  || %2d  %d   | OK", a, b, cin, s, cout);
        else
            $display("%2d %2d  %d  || %2d  %d   | NG", a, b, cin, s, cout);
    endtask

    initial begin
        $display(" a  b cin ||  s cout | OK?");
        $display("----------++---------|----");

        // ランダムに16個のパターンを試す
        repeat(16) begin
            #20;
            a = $random&15;
            b = $random&15;
            cin = $random&1;
        end
    end

    // 結果を表示
    initial forever @(a, b, cin) begin
        #0 check(a, b, cin);
    end
endmodule
```

まず、```task``` 文で計算結果が正しいかどうかを判定する処理を記述します。
4ビットの変数 ```expected``` に機械的に計算した加算結果 ```a + b + cin``` を格納し、加算器ブロックの出力 ```s``` と ```cout``` と比較します。
```cout``` は桁上がりがあるかを示す1ビットの信号なので、16倍して ```s``` と足し合わせた結果を ```expected``` と比較します。
計算結果が正しければ ```OK```、間違っていれば ```NG``` が表示されます。

```systemverilog
task check(
    input logic [3:0] a, 
    input logic [3:0] b,
    input logic       cin
);
    expected = a + b + cin;
    if (s + cout * 16 == expected)
        $display("%2d %2d  %d  || %2d  %d   | OK", a, b, cin, s, cout);
    else
        $display("%2d %2d  %d  || %2d  %d   | NG", a, b, cin, s, cout);
endtask
```

次に、乱数生成機能で、```a```、```b```、```cin``` にランダムに値を代入します。
ここでは、20サイクルごとに16パターンの入力を試しています。
それぞれの信号は ```adder_block``` に接続されているので、値が代入される度に計算結果が出力されます。
また、```forever``` 文で各信号を監視して、変化があるたびに上で定義した ```check``` タスクで計算結果の正誤を判定します。

```systemverilog
initial begin
    $display(" a  b cin ||  s cout | OK?");
    $display("----------++---------|----");

    // ランダムに16個のパターンを試す
    repeat(16) begin
        #20;
        a = $random&15;
        b = $random&15;
        cin = $random&1;
    end
end

initial forever @(a, b, cin) begin
    #0 check(a, b, cin);
end
```

それではテストをコンパイルして実行してみます。

```bash
$ iverilog -g 2012 -o test_carry_lookahead_adder test_carry_lookahead_adder.sv
$ vvp test_carry_lookahead_adder
 a  b cin ||  s cout | OK?
----------++---------|----
 4  1  1  ||  6  0   | OK
 3 13  1  ||  1  1   | OK
 5  2  1  ||  8  0   | OK
13  6  1  ||  4  1   | OK
13 12  1  || 10  1   | OK
 6  5  0  || 11  0   | OK
 5  7  0  || 12  0   | OK
15  2  0  ||  1  1   | OK
 8  5  0  || 13  0   | OK
13 13  1  || 11  1   | OK
 3 10  0  || 13  0   | OK
 0 10  1  || 11  0   | OK
 6  3  1  || 10  0   | OK
 3 11  1  || 15  0   | OK
 2 14  1  ||  1  1   | OK
15  3  0  ||  2  1   | OK
```

全てのパターンで正しく計算できているようです。

## 桁上げ先見加算器の実装
上記で実装した加算器ブロックをつなげて、桁上げ先見加算器を実装します。

```test_carry_lookahead_adder.sv```
```systemverilog
module carry_lookahead_adder
    #(parameter NBIT = 32, NBIT_BLOCK = 4)
    (
        input  logic[(NBIT - 1):0] a, b,
        output logic[(NBIT - 1):0] s,
        output logic cout
    );
    // 加算器ブロックの数
    localparam int BLOCK_NUM = NBIT / NBIT_BLOCK;

    // ブロックごとの桁上げ出力
    logic [(BLOCK_NUM - 1):0] block_cout;

    genvar i;
    generate
        // BLOCK_NUM 個の加算器ブロックを接続
        for (i = 0; i < BLOCK_NUM; i++) begin : genCarryLookaheadAdder
            localparam int start_idx = NBIT_BLOCK * i;
            localparam int end_idx = NBIT_BLOCK * (i + 1) - 1;
            adder_block ab(
                .a(a[end_idx:start_idx]), .b(b[end_idx:start_idx]), .cin(i==0?1'b0:block_cout[i - 1]),
                .s(s[end_idx:start_idx]), .cout(block_cout[i])
            ); 
        end
        assign cout = block_cout[BLOCK_NUM - 1];
    endgenerate
endmodule
```

まず、パラメータで加算器全体のビット数 ```NBIT``` とブロック当たりのビット数 ```NBIT_BLOCK``` を指定しています。
これをもとに、ブロック数 ```BLOCK_NUM``` を決定し、```for``` 文で繰り返しブロックのインスタンスを作成しています。

## 桁上げ先見加算器のテスト
桁上げ先見加算器も加算器ブロックの場合と同様、いくつか乱数を発生させて出力される計算結果が正しいかどうかを確認します。

```systemverilog
module test_carry_lookahead_adder();
    logic [31:0] a, b;
    logic [31:0] s;
    logic        cout;
    logic [32:0] expected;

    // 32ビット加算器のインスタンスを作成
    carry_lookahead_adder #(.NBIT(32)) _adder (.a(a), .b(b), .s(s), .cout(cout));

    initial begin
        $display("      a          b     ||      s     cout | OK?");
        $display("-----------------------++-----------------|-----");

        // ランダムに16個のパターンを試す
        repeat(16) begin
            #20;
            a = $random&5000000000;
            b = $random&5000000000;
        end
    end

    // 結果を表示
    initial forever @(a, b) begin
        #0;
        expected = a + b;
        if (s + cout * (1 << 32) == expected)
            $display(" %10d %10d || %10d  %d   | OK", a, b, s, cout);
        else
            $display(" %10d %10d || %10d  %d   | NG", a, b, s, cout);
    end
endmodule
```

これをコンパイルして実行してみるとちゃんと動いていそうです。

```bash
$ iverilog -g 2012 -o test -s test_carry_lookahead_adder test_carry_lookahead_adder.sv
$ vvp test
      a          b     ||      s     cout | OK?
-----------------------++-----------------|-----
   33894400      86528 ||   33980928  0   | OK
     315904  536891904 ||  537207808  0   | OK
   33649152   33918976 ||   67568128  0   | OK
  570458112  134566400 ||  705024512  0   | OK
     123392   33931264 ||   34054656  0   | OK
  704770048  168148992 ||  872919040  0   | OK
  570708480   33944064 ||  604652544  0   | OK
  671473664  570761216 || 1242234880  0   | OK
  570785792     119296 ||  570905088  0   | OK
  570814976  704672256 || 1275487232  0   | OK
  134271488   33870336 ||  168141824  0   | OK
  671113216  570724864 || 1241838080  0   | OK
  536900096  570442240 || 1107342336  0   | OK
  704659456  168042496 ||  872701952  0   | OK
   33640448  570761728 ||  604402176  0   | OK
  537223680      98816 ||  537322496  0   | OK
```